plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'
}

// See https://guides.gradle.org/creating-multi-project-builds/

allprojects {
    repositories {
        jcenter() 
    }
	group 'edu.louisville.astro.software'
}

repositories {
}

subprojects {
    version = '5.0.0-SNAPSHOT'
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:29.0-jre'
    implementation project(':ij')
    implementation project(':Astronomy_')

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13'
}

mainClassName = 'AIJ.Merged.App'

application {
    setMainClassName(mainClassName)
}

jar {
    manifest {
        attributes("Main-Class": mainClassName)
    }
    exclude("resources/outer/**") //prevent outer files being shipped twice
}

task createPlugins {
    def plugins = file("$buildDir/plugins")
    outputs.dir plugins
    doLast {
        plugins.mkdirs()
        new File(plugins, 'readme.md').write("test")
    }
}

def commonDist = project.copySpec {
    from(createPlugins) {
        into 'plugins'
    }
    // Copy astronomy_.jar to correct place in distribution
    from(file("${project(':Astronomy_').buildDir}/libs/astronomy_-${project(':Astronomy_').version}.jar")) {
        rename {String name -> return 'Astronomy_.jar'}
        into 'plugins'
    }

    from("${projectDir}/packageFiles/common") {
        into ''
    }

    into('plugins') {
        from(file("${projectDir}/packageFiles/plugins"))
    }

    // Copy ij.jar to correct place in distribution
    from(file("${project(':ij').buildDir}/libs/ij-${project(':ij').version}.jar")) {
        rename {String name -> return 'ij.jar'}
        into ''
    }
}

distributions {
    main {
        contents {
            exclude {it}
        }
    }
    windows {
        contents {
            with commonDist
            // Copy root files
            from(file("${projectDir}/packageFiles/windows")) {
                into ''
            }
        }
    }

    linux {
        contents {
            with commonDist
            // Copy root files
            from(file("${projectDir}/packageFiles/linux")) {
                into ''
            }
        }
    }

    /*macos { TODO implement, needs some special packaging

    }*/
}

// Task to copy built jars into a certain folder, for use with testing
task copyBuiltJars(dependsOn: build, type: Copy) {
    group 'AstroImageJ Developement'

    // Copy astronomy_.jar
    from(file("${project(':Astronomy_').buildDir}/libs/astronomy_-${project(':Astronomy_').version}.jar")) {
        rename {String name -> return 'Astronomy_.jar'}
        into ''
    }
    // Copy ij.jar
    from(file("${project(':ij').buildDir}/libs/ij-${project(':ij').version}.jar")) {
        rename {String name -> return 'ij.jar'}
        into ''
    }
    into "${projectDir}/out"
}

task sync(type: Sync) {
    with commonDist
    destinationDir(file("${projectDir}/AIJ-Run"))
}

// Generates a working install directory of AIJ and launches it - make sure to not att it to git!
task aijRun(dependsOn: sync, type: JavaExec) {
    group 'AstroImageJ Developement'

    workingDir(file("${projectDir}/AIJ-Run/"))

    main="-jar"
    args "${projectDir}/AIJ-Run/ij.jar"

}

linuxDistZip {
    enabled = false
}

windowsDistTar {
    enabled = false
}

distTar {
    enabled = false
}