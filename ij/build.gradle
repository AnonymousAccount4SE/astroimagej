plugins {
    id 'java'        
    id 'application'
}

dependencies {
    implementation 'gov.nasa.gsfc.heasarc:nom-tam-fits:1.15.2'
}

compileJava {
    // Set language level and binary level to 8 (binary only when building with a JDK 9+)
    // Ensures newer language features aren't used
    options.release.set(8)

    // AIJ is large, so this caches compiling results and only recompiles changed files
    options.incremental = true
}

mainClassName = 'ij.ImageJ'

jar {
    FileTree m = fileTree("${rootProject.projectDir}/src/main/resources/plugins/")
    Collection n = m.include("**/*.jar").grep()

    manifest {
        attributes("Main-Class": mainClassName)
        attributes("Class-Path": "plugins/ ${n.collect {'plugins/' + ((File)it).getName()}.join(' ')}")
    }

    // Ship nom.tam.fits
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}