plugins {
    id 'java-library'
}

dependencies {
    implementation project(':Nom_Fits')
    api "com.github.librepdf:openpdf:1.3.26"
    implementation 'org.openjdk.nashorn:nashorn-core:15.3'
}

def mainClassName = 'ij.ImageJ'

processResources {
    inputs.property "version", project.version

    filesMatching("aij_version.txt") {
        expand "version": project.version
    }
}

jar {
    FileTree m = fileTree("${rootProject.projectDir}/src/main/resources/plugins/")
    Collection n = m.include("**/*.jar").grep()

    archiveFileName = 'ij.jar'

    manifest {
        attributes("Main-Class": mainClassName,
                   "Class-Path": "plugins/ ${n.collect {'plugins/' + ((File)it).getName()}.join(' ')}")
    }

    // Ship nom.tam.fits
    dependsOn configurations.runtimeClasspath
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}